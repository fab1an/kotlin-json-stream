import org.jetbrains.dokka.gradle.DokkaTask

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version "1.9.10"
    id("convention.publication")
    id "org.jetbrains.dokka" version "1.8.20"
}

group 'com.fab1an'
version '1.1.1-SNAPSHOT'

repositories {
    mavenCentral()
}

kotlin {
    targetHierarchy.default()
    mingwX64()
    ios()
    macosArm64()
    macosX64()
    linuxX64()
    jvm {
        jvmToolchain(17)
        withJava()
        testRuns.named("test") {
            executionTask.configure {
                useJUnitPlatform()
            }
        }
    }
    js(IR) {
        nodejs()
    }
    sourceSets {
        commonMain {
            dependencies {
                api "com.squareup.okio:okio:3.5.0"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin("test")
            }
        }
    }
}

dependencies {
    dokkaPlugin("org.jetbrains.dokka:versioning-plugin:1.8.20")
}

tasks.withType(DokkaTask.class) {
    dokkaSourceSets {
        configureEach {
            externalDocumentationLink {
                url.set(new URL("https://square.github.io/okio/3.x/okio/"))
                packageListUrl.set(new URL("https://square.github.io/okio/3.x/okio/okio/package-list"))
            }
        }

    }
    String versioningPluginConfiguration = """
    {
      "olderVersionsDir": "${projectDir.toPath().resolve("documentation")}"
    }
    """
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.versioning.VersioningPlugin": versioningPluginConfiguration]
    )
}
